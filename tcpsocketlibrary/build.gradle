apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

version '1.0.7'

if (project.parent.name.equals("TcpSocketLibrary")) {
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    group 'com.algorigo.tcpsocketlibrary'

    publishing {
        publications {
            AlgorigoBle(MavenPublication) {
                artifact("$buildDir/outputs/aar/tcpsocketlibrary-release.aar")
                groupId this.group
                artifactId 'TcpSocketLibrary'
                version this.version

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.implementation.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included in the pom.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publications = ['TcpSocketLibrary']
        configurations = ['archives']
        override = true
        pkg {
            repo = 'maven'
            name = 'TcpSocketLibrary'
            userOrg = 'chagilhwan'
            description = "Tcp Socket Rx Library"
            publish = true
            publicDownloadNumbers = true
            licenses = ['Apache-2.0']
            websiteUrl = 'https://github.com/Algorigo/TcpSocketLibrary'
            vcsUrl = '${websiteUrl}.git'
            dryRun = false
            version {
                name = this.version
                desc = 'minor update'
                released = new Date()
                vcsTag = this.version
            }
        }
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 1
        versionName this.version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.android.support:appcompat-v7:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxkotlin:2.3.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'
    //ReactiveX Relay
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.0'
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.novoda:bintray-release:0.9'
    }
}
